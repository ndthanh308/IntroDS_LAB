module Max
    use int.Int
        
    let max (x : int) (y : int) : int 
      ensures { result = x \/ result = y }
      ensures { result >= x /\ result >= y }
    = if x < y then y else x 
end

module MaxList
  use int.Int
  use list.List 
  use list.Length
  use list.NthNoOpt
  use Max

  function ([]) (l : list 'a) (i : int) : 'a = nth i l

  let rec maximum (l : list int) : int 
    requires { length l > 0 } 
    ensures { forall i:int. 0 <= i < length l -> result >= l[i] }
    ensures { exists i:int. 0 <= i < length l /\ l[i] = result }
    variant { l }
  = match l with 
    | Nil -> absurd
    | Cons h Nil -> h 
    | Cons h t -> 
      let _ = assert { forall i:int. 0 <= i < length t -> 
                                l[i+1] = t[i] } in
      max h (maximum t)
    end
end